10  REM INFO.BAS - PC-BASIC 3.23 Information
15  REM Copyright (c) 2013, 2014 Rob Hagemans
20  REM Released under the GNU GPL version 3
25  '
50  KEYS.NAME$(1)="1"     : KEYS.WHAT$(1)="About"    : FILE$(1)="@:ABOUT"
55  KEYS.NAME$(2)="2"     : KEYS.WHAT$(2)="Legal"    : FILE$(2)="@:COPYING"
60  KEYS.NAME$(3)="3"     : KEYS.WHAT$(3)="GPLv3"    : FILE$(3)="@:GPL3"
62  KEYS.NAME$(4)="4"     : KEYS.WHAT$(4)="CC-BY-SA" : FILE$(4)="@:CC-BY-SA"
65  KEYS.NAME$(5)="5"     : KEYS.WHAT$(5)="Doc"      : FILE$(5)="@:HELP"
70  KEYS.NAME$(6)="Q"     : KEYS.WHAT$(6)="Quit"
75  KEYS.NAME$(7)="S"     : KEYS.WHAT$(7)="Search"
80  KEYS.NAME$(8)="SPACE" : KEYS.WHAT$(8)="Line"
85  KEYS.NAME$(9)="ENTER" : KEYS.WHAT$(9)="Page"
90  KEYS.LEN = 9 : FILES.NUM = 5 : K = 1
99  '
100 REM main loop
105   SCREEN 0,0,0,0 : WIDTH 80 : KEY OFF
110   WHILE -1
111     IF K > 0 AND K <= FILES.NUM THEN CLS: COUNT=0
112     IF K = 1 THEN NM$ = "@:VERSION": GOSUB 13000: COLOR 0,7: GOSUB 10000: COLOR 7,0: PRINT: COUNT=2
115     IF K > 0 AND K <= FILES.NUM THEN NM$ = FILE$(K): GOSUB 13000
120     GOSUB 10000 ' display text file
125     GOSUB 1000  ' display bottom line
130     GOSUB 1200  ' wait for key, fill K$ and K
135     IF K$ = "S" THEN GOSUB 15000     ' search
140     IF K$ = "Q" THEN GOSUB 1400      ' quit
150     IF K$ = " " THEN GOSUB 14000     ' scroll line
160     IF K$ = CHR$(13) THEN CLS : COUNT = 0 ' force loading next page
170   WEND 
990 END 
999 '
1000 REM bottom line
1010   VIEW PRINT : LOCATE 25,1
1020   FOR I = 1 TO KEYS.LEN
1030     PRINT KEYS.NAME$(I);
1040     COLOR 0,7 : PRINT " " KEYS.WHAT$(I); " "; 
1050     COLOR 7,0
1060   NEXT 
1070   COLOR 0,7 : PRINT SPC(81-POS(0));
1080   COLOR 7,0 
1090   VIEW PRINT 1 TO 24
1095 RETURN 
1099 '
1200 REM Press a key
1220   K$ = "" : WHILE K$ = "" : K$ = INKEY$ : WEND 
1230   IF K$ >= "a" AND K$ <= "z" THEN K$ = CHR$(ASC(K$)-32)
1240   K = VAL(K$)
1250 RETURN 
1299 '
1400 REM exit
1405   CLS : KEY ON 
1410   END 
9999 '
10000 REM display text file
10020   WHILE NOT EOF(1) AND COUNT<23
10030     LINE INPUT #1, LIN$
10040     PRINT LIN$;:IF CSRLIN<COUNT+2 THEN PRINT CHR$(13);
10050     COUNT=COUNT+1
10070   WEND 
10110 RETURN 
10199 '
13000 REM swap files
13010   CLOSE #1
13020   OPEN NM$ FOR INPUT AS 1
13050 RETURN 
13999 '
14000 REM display one line from text file
14010   COUNT = COUNT+1
14020   LOCATE 24,1
14024   IF EOF(1) THEN RETURN 
14030   LINE INPUT #1, LIN$
14040   PRINT LIN$
14050 RETURN 
14999 '
15000 REM search
15030   VIEW PRINT:LOCATE 25,1:COLOR 0,7:PRINT SPC(79);
15035   LOCATE 25,3:PRINT "Search: ";:COLOR 7,0:INPUT ;"", COMMAND$
15039   VIEW PRINT 1 TO 24:CLS 
15040   GOSUB 15300
15090 RETURN 
15099 '
15300 REM Find command in text file, then display
15305   CLOSE #1
15310   OPEN NM$ FOR INPUT AS #1
15320   WHILE NOT EOF(1)
15330     LINE INPUT #1, LIN$
15340     IF LEFT$(LIN$,LEN(COMMAND$))=COMMAND$ THEN PRINT LIN$: COUNT=1: GOTO 15390
15370   WEND 
15390 RETURN 
15999 '

